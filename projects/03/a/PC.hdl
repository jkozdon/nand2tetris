// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Set load bit for register
    Or (a=reset, b=load, out=do0);
    Or (a=do0, b=inc, out=do);

    // incremenent current value by one
    Inc16 (in=curout, out=doinc);

    // Select whether input or increment
    Mux16 (a=doinc, b=in, sel=load, out=doincload);

    // Select whether zero or previous
    Mux16 (a=doincload, b=false, sel=reset, out=doincloadreset);

    // update register
    Register (in=doincloadreset, load=do, out=out, out=curout);
}
